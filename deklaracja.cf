-- JPP 2014/15, zad 2 - deklaracja jezyka, Michal Wos, mw336071
-- szkielet zrodlo: https://github.com/BNFC/bnfc/blob/master/examples/C/C.cf

entrypoints Program, Stm, Exp ;

Progr.  Program ::= [Compund_content] ;

ScompContentEmpty.	Compund_content ::= ;
ScompContentStm.	Compund_content ::= Stm;
ScompContentDec.	Compund_content ::= Dec;

(:[]).   [Compund_content] ::= Compund_content ;
(:).     [Compund_content] ::= Compund_content [Compund_content];


-- DECLARATIONS

VariableDec. Dec ::= Variable ";" ;
FuncDec.     Dec ::= Function ;

Tbool.        Type_specifier ::= "bool";
Tint.         Type_specifier ::= "int";

DecBase.  Dec_base ::= Type_specifier Ident;

InitDec.               Variable ::= Initialized_variable ;
UninitDec.             Variable ::= Uninitialized_variable ;

UninitSimpleTypeDec.   Uninitialized_variable ::= Dec_base ;
UninitArr.             Uninitialized_variable ::= Dec_base "[" Constant_expression "]";

InitSimpleTypeDec.     Initialized_variable ::= Dec_base "=" Initializer;
InitArr.               Initialized_variable ::= Dec_base "[" "]" "=" "{" [Initializer] "}";

InitExpr.    Initializer ::= Exp2 ;
separator nonempty Initializer "," ;

NoParamFunc.	Function ::= Dec_base "(" ")" Namespace_stm ;
ParamFunc.	    Function ::= Dec_base "(" [Param] ")" Namespace_stm ;

separator nonempty Param "," ;

FuncParam.   Param ::= Uninitialized_variable ;


-- STATEMANTS

LabelS.   Stm ::= Labeled_stm ;
SpaceS.   Stm ::= Namespace_stm ;
ExprS.    Stm ::= Expression_stm ;
SelS.     Stm ::= Selection_stm ;
IterS.    Stm ::= Iter_stm ;
JumpS.    Stm ::= Jump_stm ;
PrintS.   Stm ::= Print_stm ;

Scase.    Labeled_stm ::= "case" Constant_expression ":" Stm ;
Sdefault. Labeled_stm ::= "default" ":" Stm;

Snamespace. Namespace_stm ::= "{" [Compund_content] "}" ;

Sexpr.      Expression_stm ::= Exp ";" ;

Sif.      Selection_stm ::= "if" "(" Exp ")" Stm ;
SifElse.  Selection_stm ::= "if" "(" Exp ")" Stm "else" Stm ;
Sswitch.  Selection_stm ::= "switch" "(" Exp ")" Stm ;

Swhile.     Iter_stm ::= "while" "(" Exp ")" Stm;
Sfor.       Iter_stm ::= "for" "(" Exp_or_empty ";" Exp_or_empty ";" Exp_or_empty ")" Stm;
SemptyExp.    Exp_or_empty ::= ;
SnonemptyExp. Exp_or_empty ::= Exp ;

Scontinue.  Jump_stm ::= "continue" ";" ;
Sbreak.     Jump_stm ::= "break" ";" ;
Sreturn.    Jump_stm ::= "return" Exp ";" ;

SPrint.	  Print_stm ::= "print" "(" Exp2 ")" ";";

(:[]).   [Stm] ::= Stm ;
(:).     [Stm] ::= Stm [Stm];


-- EXPRESSIONS

Ecomma.      Exp   ::= Exp "," Exp2;
Eassign.     Exp2  ::= Exp15 Assignment_op Exp2;
Elor.        Exp4  ::= Exp4 "||" Exp5;
Eland.       Exp5  ::= Exp5 "&&" Exp6;
Eeq.         Exp9  ::= Exp9 "==" Exp10;
Eneq.        Exp9  ::= Exp9 "!=" Exp10;
Elthen.      Exp10 ::= Exp10 "<" Exp11;
Egrthen.     Exp10 ::= Exp10 ">" Exp11;
Ele.         Exp10 ::= Exp10 "<=" Exp11;
Ege.         Exp10 ::= Exp10 ">=" Exp11;				
Eplus.       Exp12 ::= Exp12 "+" Exp13;
Eminus.      Exp12 ::= Exp12 "-" Exp13;
Etimes.      Exp13 ::= Exp13 "*" Exp14;
Ediv.        Exp13 ::= Exp13 "/" Exp14;
Epreinc.     Exp15 ::= "++" Ident;
Epredec.     Exp15 ::= "--" Ident;
Epreop.      Exp15 ::= Unary_operator Exp14;
Earray.      Exp16 ::= Ident "[" Exp "]" ;
Efunk.       Exp16 ::= Ident "(" ")";
Efunkpar.    Exp16 ::= Ident "(" [Exp2] ")";
Epostinc.    Exp16 ::= Ident "++";
Epostdec.    Exp16 ::= Ident "--";
Evar.        Exp17 ::= Ident;
Econst.      Exp17 ::= Constant;

Ebool.         Constant ::= CBool;
Eint.          Constant ::= Integer;  

BTrue.	CBool ::= "true";
BFalse.	CBool ::= "false";

Especial. Constant_expression ::= Exp3;

--_. Exp   ::= Exp2 ;
--_. Exp2  ::= Exp3 ;
--_. Exp3  ::= Exp4 ;
--_. Exp4  ::= Exp5 ;
--_. Exp5  ::= Exp6 ;
--_. Exp6  ::= Exp7 ;
--_. Exp7  ::= Exp8 ;
--_. Exp8  ::= Exp9 ;
--_. Exp9  ::= Exp10 ;
--_. Exp10 ::= Exp11 ;
--_. Exp11 ::= Exp12 ;
--_. Exp12 ::= Exp13 ;
--_. Exp13 ::= Exp14 ;
--_. Exp14 ::= Exp15 ;
--_. Exp15 ::= Exp16 ;
--_. Exp16 ::= Exp17 ;
--_. Exp17 ::= "(" Exp ")" ;

coercions Exp 17 ;

Plus.        Unary_operator ::= "+" ;
Negative.    Unary_operator ::= "-" ;
Logicalneg.  Unary_operator ::= "!" ;

(:[]).   [Exp2] ::= Exp2 ;
(:).     [Exp2] ::= Exp2 "," [Exp2];

Assign.       Assignment_op ::= "=" ;
AssignMul.    Assignment_op ::= "*=" ;
AssignDiv.    Assignment_op ::= "/=" ;
AssignAdd.    Assignment_op ::= "+=" ;
AssignSub.    Assignment_op ::= "-=" ;

comment "//";
